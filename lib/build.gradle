plugins {
    id 'java-library'
    id 'jacoco' // Adds jacocoTestReport task for coverage.
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.71'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        showStackTraces = true
        exceptionFormat = 'full'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'fi.protonode'
            artifactId = 'certy'

            from components.java

            pom {
                name = 'certy'
                description = 'Java library for generating certificates for tests.'
                url = 'https://github.com/tsaarni/certy'
                developers {
                    developer {
                        id = 'tsaarni'
                        name = 'Tero Saarni'
                        email = 'tero.saarni@gmail.com'
                        organizationUrl = 'https://github.com/tsaarni/'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:tsaarni/certy.git'
                    developerConnection = 'scm:git:ssh://github.com:tsaarni/certy.git'
                    url = 'https://github.com/tsaarni/certy'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'undefined'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'undefined'
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}
